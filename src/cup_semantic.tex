\section{The Symbol Class}
In CUP each symbol in the stack is an object of class Symbol (\code{cup/java_cup/runtime/Symbol.java}).
It contains the following information:
\begin{itemize}
    \item
    a number uniquely identifying the symbol (\code{public int sym});
    \item
    the state in which the parse is (\code{public int parse_state});
    \item
    two integers that are used to pass the line and column numbers from the scanner to the parser (\code{public int left}, \code{public int right});
    \item
    an object of class Object to handle semantics (\code{public Object value}).
\end{itemize}

\subsection{Passing Semantic Values to the Parser}
Example: scanner.jflex

Symbol constructors:
\begin{itemize}
    \item
    \code{public Symbol(int sym_id)};
    \item
    \code{public Symbol(int sym_id, int left, int right)};
    \item
    \code{public Symbol(int sym_id, Object o)};
    \item
    \code{public Symbol(int sym_id, int left, int right, Object o)}
\end{itemize}
Symbol and semantic values:
\begin{lstlisting}
[a-zA-Z][a-zA-Z0-9]* {return new Symbol(sym_id, new String(yytext()));}
\end{lstlisting}
Symbol, line number, column number, and semantic value:
\begin{lstlisting}
%{
    private Symbol symbol(int type, Object value) {
        return new Symbol(type, yyline, yycolumn, value);
    }
%}
\end{lstlisting}

\begin{lstlisting}
[a-zA-Z][a-zA-Z0-9]* {return new Symbol(sym_id, new String(yytext()));}
\end{lstlisting}
or equivalently:
\begin{lstlisting}
[a-zA-Z][a-zA-Z0-9]* {return new Symbol(sym_id, yyline, yycolumn, new String(yytext()));}
\end{lstlisting}

\section{Specifying Nodes Types}
CUP must know the type of the semantic value of each symbol; it uses the following definition of terminals and non-terminals:
\begin{itemize}
    \item
    \code{terminal <Object> <list_of_terminals>;}
    \item
    \code{non terminal <Object> <list_of_non-terminals>;}
\end{itemize}
\code{<Object>} is the class of the object associated to a given symbol.

Example:
\begin{itemize}
    \item
    \code{terminal String ID;} an object of class String will be associated to \code{ID};
    \item
    \code{terminal Integer NUM;}
    \item
    \code{non terminal MyObject var;} will have something like this:
    \begin{lstlisting}
class MyObject {
    public String var_name;
    public String var_type;
}
    \end{lstlisting}
\end{itemize}

\section{Using Semantic Values}
Given a set of productions:
\begin{lstlisting}
E ::= E PLUS T
    | E MINUS T
\end{lstlisting}
One can refer to the semantic value of each symbol by adding labels to the symbol of interest.
A label is constituted by the ``\code{:}'' character, followed by a name.
\begin{lstlisting}
E ::= E:m1 PLUS T:m2
    | E:m1 MINUS T:m2
\end{lstlisting}
Within each production, the labels can be normally used as objects of the class specified in the definition of terminals and non-terminals.
\begin{lstlisting}
E ::= E:m1 PLUS T:m2 {: System.out.println(m1 + " + " + m2); :}
    | E:m1 MINUS T:m2 {: System.out.println(m1 + " - " + m2); :}
\end{lstlisting}

\section{Action and \code{RESULT}}
An action can be associated to each production (\code{\{:/*java code*/:\}}) and is executed every time the corresponding production is reduced.
The action updates the semantic values of each symbol.
For each production, the result object (of class Object) is defined.

\code{RESULT} represents the result of the semantic rules contained in the action and is therefore associated to the symbol in the left hand side of the production.

\subsection{Calculating Synthesized Attributes}
Given the algebraic expression grammar, the following rules assigns to the symbol ``\code{E}'' the sum or the subtraction of the values of the addends/subtrahends.
\begin{lstlisting}[frame=single]
non terminal Integer E;

E ::= E:m1 PLUS T:m2 {:
        RESULT = new Integer(m1.intValue() + m2.intValue());
    :}
    | E:m1 MINUS T:m2 {:
        RESULT = new Integer(m1.intValue() - m2.intValue());
    :}
\end{lstlisting}
Note that \code{RESULT} must be assigned n object of class Integer (\code{new Integer()}); mathematical operators work on numbers, not objects (\code{m1.intValue()}).

It is possible to propagate more than one semantic value through \code{RESULT}.

Example:
\begin{lstlisting}[frame=single]
terminal TO, TC;
terminal String Identifier;
terminal Integer Args;

non terminal Object[] func;
non terminal goal;

goal ::= func:a {:
    System.out.println("Function name: " + a[0] + ", number of parameters: " + a[1]);
:};

func ::= Identifier:a TO Args:b TC {:
    RESULT = new Object[2];
    RESULT[0] = new String(a);
    RESULT[1] = new Integer(b);
:};
\end{lstlisting}
Alternatively, one can write a class that contains all the required information.

Example:
\begin{lstlisting}[frame=single]
action code {:
    class MyFunc {
        public String id;
        public Integer args;
        MyFunc(String id, String args) {
            this.id = id;
            this.args = args;
        }
    }
:}

non terminal MyFunc func;

goal ::= func:a {:
    System.out.println("Function name: " + a.id + ", number of parameters: " + a.args);
:};

func ::= Identifier:a TO Args:b TC {:
    RESULT = new MyFunc(a, b);
:};
\end{lstlisting}

\section{Parser Debugging}
A series of options are available in CUP to visualize the parser's internal structures:
\begin{itemize}
    \item
    \code{-dump_grammar} prints the list of terminals, non-terminals, and productions;
    \item
    \code{-dump_states} prints the states graph;
    \item
    \code{-dump_table} prints the action table and the reduce table;
    \item
    \code{-dump} prints all the information.
\end{itemize}
The parser can be executed in debug mode (all the actions performed to analyze the input sequence are printed).

Example:

Normal mode:
\begin{lstlisting}
                        Yylex l = new Yylex(new FileReader(file));
                        parser p = new parser(l);
                        Object result = p.parse();
\end{lstlisting}

$$
\Downarrow
$$

Debug mode:
\begin{lstlisting}
                        Yylex l = new Yylex(new FileReader(file));
                        parser p = new parser(l);
                        Object result = p.debug_parse();
\end{lstlisting}

\section{Inherited Attributes}
Inherited attributes are useful to express the dependency of a production on its context.

Example:
\begin{lstlisting}
a,b: int;
\end{lstlisting}
\begin{align*}
& D \to L \, ':' \, T \, ';' \\
& L \to L_1 \, ',' \, id \\
& L \to id \\
& T \to 'integer'
\end{align*}
\begin{lstlisting}
L.type = T.type
L1.type = L.type; new_var(id.name, L.type)
new_val(id.name, L.type)
T.type = type_int
\end{lstlisting}

\section{L-Attribute Grammar}
The order in which attributes are evaluated depends on the order in which the parse tree is created or visited.
Usually, parser follows the same order of the depth-first visit algorithm.\footnote{\url{http://en.wikipedia.org/wiki/Depth-first_search}}

An L-Attribute grammar is defined as a grammar whose attributes values can be calculated by means of a depth-first visit of the parse tree.
In these grammars, information propagates from left to right (within the parse tree).
In the previous example, the grammar is not an L-Attribute grammar (information propagates from right to left); CUP manages only L-Attributes grammar.

Example:
\begin{lstlisting}
int a, b;
\end{lstlisting}
\begin{align*}
& D \to T \, L \, ';' \\
& L \to L_1 \, ';' \, id \\
& L \to id \\
& T \to 'int'
\end{align*}
\begin{lstlisting}
L.type = T.type
L1.type = L.type; new_var(id.name, L.type)
new_val(id.name, L.type)
T.type = type_int
\end{lstlisting}

\subsection{Calculating Inherited Attributes}
In a bottom-up parser, memory is not allocated in the semantic stack until the corresponding symbol is recognized.
This is a troublesome for handling inherited attributes.
If the grammar is an L-Attribute one, and the semantic rules for inherited attributes are copy-rules, the issue can be tackled in a relatively simple way.
If rules are more complex than this, it is possible to use \emph{markers} (non-terminals that are expanded with $\varepsilon$ symbol).

Example: a production with a copy-rule.
\begin{align*}
& D \to T \, lid \, S \\
& lid = ID
\end{align*}
\begin{lstlisting}
lid.type = T.type
var(ID.name, lid.type)
\end{lstlisting}
Stack before \code{lid} is reduced:
\begin{figure}[H]
    \centerline{\includegraphics[width=0.3\textwidth]{img/33.pdf}}
\end{figure}

To access to the semantic values stored in the stack in a given position, use the function \code{Object stack(int position)}.
\begin{itemize}
    \item
    \code{stack(0)} is the semantic value associated with the symbol in the top of the stack;
    \item
    \code{stack(n)} is the semantic value associated with the symbol in the position \code{top + n} of the stack.
\end{itemize}

\begin{lstlisting}
parser code {:
    public Object stack(int position) {
        // returns the object at the specified position from the top of the stack
        return(((Symbol) stack.elementAt(top + position)).value);
    }
:}
\end{lstlisting}

Example:
\begin{figure}[H]
    \centerline{\includegraphics[width=0.3\textwidth]{img/34.pdf}}
\end{figure}
The \code{type} attribute of \code{lid} is inherited.
Its value is present in the semantic stack (at position ``\code{T}'') before \code{lid} is created.
However, it is beyond the semantic scope of the \code{lid} production.
With the assumption that the \code{lid} symbol is always preceded by a type identifier:
\begin{lstlisting}
lid ::= ID.name {:
    String type = (String) parser.stack(-1);
    RESULT = new String(type);
    add_id(name, RESULT);
:};
\end{lstlisting}
Stack:
\begin{figure}[H]
    \centerline{\includegraphics[width=0.3\textwidth]{img/33.pdf}} %same picture, no need for a new graph
\end{figure}

\subsubsection{Calculating Inherited Attributes by means of Markers}
Example:
\begin{figure}[H]
    \centerline{\includegraphics[width=0.4\textwidth]{img/35.pdf}}
\end{figure}
If the rule ``\code{lid ::= ID CM lid}'' is added, it is not true anymore that \code{lid} is always preceded by a type identifier.
In the case of the rule ``\code{lid ::= ID;}'' the symbol preceding \code{ID} in the sack before reducing is \code{CM} and not the type.

By adding an empty rule (\emph{marker}), one can ensure that the rule ``\code{lid ::= ID}'' is preceded by a type semantic value (the marker is used to move a semantic value in a desired position in the stack).
Note that in order to have easier semantic actions is always better to have left recursive lists:\footnote{Anyhow, in some grammars, also using recursive left-recursive lists, markers are needed.}
\begin{lstlisting}
lid ::= lid CM ID
    | ID;
\end{lstlisting}

Example:
\begin{lstlisting}
lid ::= {:
    RESULT = (String) parser.stack(-1);
    add_id(name, RESULT);
:};

lid ::= ID:name CM Empty Lid {:
    RESULT = (String) parser.stack(-1);
    add_id(name, RESULT);
:};

Empty ::= {:
    RESULT = (String) parser.stack(-2);
:};
\end{lstlisting}

\section{Intermediate Actions}
In order to avoid explicitly introducing a non-terminal with an empty production, one can use in the right-hand side of the production an \emph{intermediate action}.
Intermediate actions are automatically substituted with a non-terminal symbol, which in turn is given by an empty production.

Example: the following code
\begin{lstlisting}
lid ::= ID:name CM Empty lid;

Empty ::= ;
\end{lstlisting}
can be rewritten as
\begin{lstlisting}
lid ::= ID:name CM {:
        RESULT = (String) parser.stack(-2);
    :} lid {:
        RESULT = (String) parser.stack(-1);
        add_id(name, RESULT);
    :};
\end{lstlisting}

Example: marker

Parser
\begin{lstlisting}[frame=single]
import java_cup.runtime.*;

%%

%cup
%unicode

NL = \n|\r|\r\n
ID = [a-zA-Z][a-zA-Z0-9]*
TYPE = int|float|char|double

%%

"," {
    return new Symbol(sym.CM);
}

";" {
    return new Symbol(sym.S);
}

{TYPE} {
    return new Symbol(sym.TYPE, new String(yytext()));
}

{ID} {
    return new Symbol(sym.ID, new String(yytext()));
}

{NL}|" "|\t {
    ;
}
\end{lstlisting}
Scanner
\begin{lstlisting}[frame=single]
import java_cup.runtime.*;

parser code {:
    // returns semantic value of symbol in position "position"
    public Object stack(int position) {
        return (((Symbol) stack.elementAt(tos + position)).value);
    }
:};

terminal VIR, PV;
terminal String TYPE, ID;

non terminal goal, list_decl;
non terminal String decl, lid;

start with goal;

goal ::= {:
    System.out.println("Parser: recognized grammar");
:};

list_decl ::=
    | list_decl decl;

decl ::= TYPE lid:x PV {:
    System.out.println("Parser: found declaration of type: " + x);
:};

lid ::= ID:name VIR {:
        RESULT = (String) parser.stack(-2);
    :} lid {:
        RESULT = (String) parser.stack(-1);
        System.out.println("Parser: var(" + name +", " + RESULT +")");
    :};
\end{lstlisting}

\section{Transforming the Grammar}
It is possible to avoid using inherited attributes by transforming the grammar.

Example:
\begin{align*}
    D &\to L \, ';' \, T\\
    T &\to int \, | \, float\\
    L &\to L \, ',' \, id \, | \, id\\
    \\
    & \qquad \Downarrow\\
    \\
    D &\to id \, L\\
    L &\to ',' \, id \, L \, | \, ':' \, T\\
    T &\to int \, | \, float
\end{align*}

\section{Handling Semantic Errors}
Semantic errors are usually handled in the actions associated to productions.
Usually actions verify that:
\begin{itemize}
    \item
    operands types are compatible;
    \item
    variables and functions are declared;
    \item
    parameters passed to a function are coherent with the function prototype.
\end{itemize}

\subsubsection{Intermediate Code Generation: the \code{while} statement}
As an example of intermediate code generation, a simple \code{while} statement
\begin{lstlisting}
while_c ::= WHILE (a > 0) {% STMT %}
\end{lstlisting}
can be translated in the following intermediate code:
\begin{lstlisting}
L0: EVAL COND
    GOTOF L1
    STMT
    GOTO L0
L1:
\end{lstlisting}
where \code{GOTOF} is a jump instruction\footnote{In the x86 assembly language, the \code{JMP} instruction performs an unconditional jump. Such an instruction transfers the flow of execution by changing the instruction pointer register.} executed only if the result of the above \code{eval} command is 0 (\code{false}).
\code{L0} and \code{L1} are labels.

A possible solution of the while problem that uses inherited attributes is:
\begin{lstlisting}
while_c ::= WHILE cond NT0:x STMT {:
    Integer[] I = x;
    System.out.println("GOTO L" + I[0]);
    System.out.println("L" + I[1] + ":");
:};

NT0 ::= {:
    RESULT = new Integer[2];
    RESULT[0] = genLabel(); // L0
    RESULT[1] = genLabel(); // L1
    System.out.println("L" + RESULT[0] + ";");
    System.out.println("EVAL " + parser.stack(0));
    System.out.println("GOTOF L" + RESULT[1]);
:};
\end{lstlisting}